buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
}

group 'pl.grzeslowski.smarthome.rf24'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.8

sourceSets {
	examples {
		java {
			srcDir "src/main/examples"
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	testCompile group: 'junit', name: 'junit', version: '4.11'

	examplesCompile group: 'commons-cli', name: 'commons-cli', version: '1.3'
	examplesCompile sourceSets.main.output
}

ext {
	javaJni = ext.has('javaJni') ? javaJni : '/usr/lib/jvm/java-8-openjdk-amd64/include'
	javaJniPlatformSpecyfic = ext.has('javaJniPlatformSpecyfic') ? javaJniPlatformSpecyfic : "$javaJni/linux"

	pathToUtility = 'native/RF24/utility'
	utilityFolder = pathToUtility + (ext.has('utilityFolder') ? utilityFolder : "/RPi")
}

task compileSwig(type: Exec) {
	final swigPackage = "${project.group}.generated"
	commandLine "bin/swig-3.0.10/swig"
	//noinspection GroovyAssignabilityCheck
	args "-c++", '-java', '-outdir', "src/main/java/${swigPackage.replace(".", "/")}", "-package", "${swigPackage}", "swig-config/rf24bcmjava.i"
	standardOutput = new ByteArrayOutputStream()
}

task cleanSwig(type: Delete) {
	delete fileTree(dir: "out/main/java")
	followSymlinks = false
}

task copyIncludesForWrapper(type: Copy) {
	from utilityFolder
	into pathToUtility
}

task cleanAfterCompileWrapper(type: Delete) {
	delete fileTree(dir: pathToUtility).include('*')
	delete file('rf24bcmjava_wrap.o')
}
tasks.clean.dependsOn += tasks.cleanAfterCompileWrapper

task compileWrapper(type: Exec, dependsOn: 'copyIncludesForWrapper') {
	commandLine "gcc"
	//noinspection GroovyAssignabilityCheck
	args "-fPIC", "-c", "swig-config/rf24bcmjava_wrap.cxx", "-I$javaJni", "-I$javaJniPlatformSpecyfic", "-Inative/RF24", "-Inative/RF24/utility"
}

task linkWrapper(type: Exec, dependsOn: 'compileWrapper') {
	commandLine "ld"
	//noinspection GroovyAssignabilityCheck
	args "-G", "rf24bcmjava_wrap.o", "-o", "build/librf24bcmjava.so"
}
tasks.linkWrapper.dependsOn += tasks.compileWrapper

allprojects {
	tasks.withType(Exec) {
		doFirst {
			println "Cmd to be executed:"
			println " > " + commandLine.join(' ')
		}
	}
}