buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
}

plugins {
	id "com.jfrog.bintray" version "1.7"
}

group 'pl.grzeslowski.smarthome.rf24'
version '1.0.0-SNAPSHOT'

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	testCompile(
			'junit:junit:4.12',
			'org.codehaus.groovy:groovy-all:2.4.4',
			'org.spockframework:spock-core:1.0-groovy-2.4')
}

ext {
	javaJni = ext.has('javaJni') ? javaJni : '/usr/lib/jvm/jdk-8-oracle-arm32-vfp-hflt/include'
	javaJniPlatformSpecific = ext.has('javaJniPlatformSpecific') ? javaJniPlatformSpecific : "$javaJni/linux"
	headersDir = ext.has('headersDir') ? headersDir : "RPi"
	wrapperBuildDir = ext.has('wrapperBuildDir') ? wrapperBuildDir : "$project.buildDir/wrapper"
	swigPackage = "${project.group}.generated"
}

apply from: 'swig.gradle'
apply from: 'rf24.gradle'

publishing {
	publications {
		Rf24Publication(MavenPublication) {
			from components.java
			groupId 'pl.grzeslowski.smarthome'
			artifactId 'rf24'
			version project.version.replaceAll("-SNAPSHOT", '')
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('BINTRAY_KEY')

	publications = ['Rf24Publication']
	dryRun = project.hasProperty('dryRun')
	publish = true
	pkg {
		repo = 'bigboy'
		name = project.group
		desc = 'This is Java JNI wrapper on RF24 library.'
		websiteUrl = 'https://github.com/magx2/rf24-java-jni'
		issueTrackerUrl = 'https://github.com/magx2/rf24-java-jni/issues'
		vcsUrl = 'https://github.com/magx2/rf24-java-jni.git'
		licenses = ['MIT']
		labels = ['RF24', 'Raspberry', 'PI', 'RPi', 'NRF24L01']
		publicDownloadNumbers = true

		githubRepo = 'https://github.com/magx2/rf24-java-jni' //Optional Github repository
		githubReleaseNotesFile = 'README.md' //Optional Github readme file

		//noinspection GroovyAssignabilityCheck
		version {
			name = project.version.replaceAll("-SNAPSHOT", '') //Bintray logical version name
			vcsTag = 'v' + project.version.replaceAll("-SNAPSHOT", '')
			gpg {
				sign = true
				passphrase = project.hasProperty('rf24Passphrase') ? project.property('rf24Passphrase') : System.getenv('RF24_PASSPHRASE')
			}
		}
	}
}

allprojects {
	tasks.withType(Exec) {
		doFirst {
			println "Exec> " + commandLine.join(' ')
		}
	}
}

tasks.clean.dependsOn {
	tasks.findAll { task -> task.name.matches("clean.+") }
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.1'
}