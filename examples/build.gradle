buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.github.rholder:gradle-one-jar:1.0.4'
	}
}

group rootProject.group
version rootProject.version

apply plugin: 'java'
apply plugin: 'gradle-one-jar'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
	examples = ['Rf24PingPongServer', 'Rf24PingPongClient']
	slf4j = '1.7.21'
}

dependencies {
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3'
    compile rootProject
	compile group: 'org.slf4j', name: 'slf4j-api', version: slf4j
	compile group: 'org.slf4j', name: 'slf4j-simple', version: slf4j
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
}


// Create Jar and run task
project.examples.forEach { String taskName ->
	final jarTaskName = "jar${taskName}Example"
	tasks.create(name: jarTaskName, type: OneJar) {
		//noinspection GroovyAssignabilityCheck
		group 'Build examples JARs'
		mainClass = "pl.grzeslowski.smarthome.rf24.examples.${taskName}Example"
		archiveName = "$project.name-${taskName.toLowerCase()}-example.jar"
	}
	tasks[jarTaskName].dependsOn rootProject.tasks.compileRf24

	final runTaskName = "run${taskName}Example"
	tasks.create(name: runTaskName, type: Exec) {
		group 'Run Examples'
		workingDir "$project.buildDir/libs"
		commandLine "sudo", "java", "-jar", "-Djava.library.path=$rootProject.wrapperBuildDir", tasks[jarTaskName].archiveName
	}
	tasks[runTaskName].dependsOn += tasks[jarTaskName]
}

